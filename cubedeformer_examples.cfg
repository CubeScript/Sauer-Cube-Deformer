// Cube Deformer examples, by @Salatiel#5274
// version: 23/01/2021

.cubedeformer.arrowplane = [
  allfaces 0
  gotoface 0 [
    gotoedge 0 [
      editface 4 2
      flip
      editface 4 2
      rotate 1
      editface 4 2; flip; editface 4 2
      rotate 1
      editface 4 2; flip; editface 4 2
      gotoface 2 [
        vcolor 0 0 1
        gotoedge 0 [
          editface 8 2
          rotate 1
          gotoface 1 [
            gotoedge 0 [
              editface 8 2
              vcolor 1 0 0
              copy
              movesel 1 1
              movesel -1 0
              paste
              rotate 1
              gotoface 2 [
                reorient
                editface -2 0
                flip
                editface -2 0
                gotoface 1 [
                  rotate -1
                ]
              ]
            ]
          ]
        ]
      ]
    ]
  ]
]

.cubedeformer.pyramid_slow = [
  allfaces 0
  local faces
  faces = [[0 0 4][0 1 4][3 0 4][3 1 4][1 0 4][1 1 4][2 0 4]]
  loop face 7 [
    sleep (* $face 300) [
      editfaceedge @(at $faces $face) [
        vcolor @@(at [[1 0 0][0 1 0][0 0 1][1 0 1][1 1 0][0 1 1]] (at $faces $face))
      ]
    ]
  ]
]

.cubedeformer.pyramid_fast = [
  gotoface 0 [
    gotoedge 0 [
      loop r 2 [
        editface 4 2
        rotate -1
        editface 4 2
        rotate 1
        flip
      ]

      sleep 400 [
        gotoface 3 [
          gotoedge 0 [
            loop r 2 [
              editface 4 2
              flip
            ]
          ]
        ]
      ]
    ]
  ] 0
]

.cubedeformer.twistedcolumn_slow = [
  allfaces 0
  loop column 8 [
    sleep (* $column 2400) [
      loop face 4 [
        sleep (* $face 600) [
          gotoface @face [
            gotoedge 1 [
              local facecolor
              facecolor = (at [[0.11 0.71 0.57][1 0.51 0.11][1 0 0.67][0.11 0.10 0.11]] @@@face)
              vcolor (at $facecolor 0) (at $facecolor 1) (at $facecolor 2)
              vscroll 0 10000

              editface (? (= @@@@@column 0) 1 2) 2

              if (= @@@face 3) [
                copy; movesel 1 2; paste
                gotosel
                gotoface 3 [
                  rotate 1; rotate 1; flip
                ] 0
              ]
            ]
          ] 0
        ]
      ]
    ]
  ]
]

.cubedeformer.twistedcolumn_fast = [
  gotoface 0 [
    gotoedge 1 [
      loop e 2 [
        editface 1 2
        if (= $e 0) [
          loop r 3 [rotate 1]
          flip
        ]
      ]
      loop c 8 [
        copy
        movesel 1 2
        paste
        editface 1 2
        rotate -1
        editface 1 2
        flip
        loop r 2 [
          rotate -1
          editface 1 2
        ]
        rotate -1
        flip
      ]
      movesel -8 2
      sleep 500 [
        gotoface 2 [
          gotoedge 1 [
            editface 1 2
            rotate 1
            flip
            editface 1 2
            rotate -1
            flip
            copy
            loop c 3 [
              movesel 1 2
              editface (+ $c 2) 2
              rotate 1
              flip
              editface (+ $c 2) 2
              rotate 1
              editface (+ $c 1) 2
              flip
              rotate 1
              editface (+ $c 1) 2
              rotate 1
            ]
            selextend
            copy
            movesel 4 2
            paste
            rotate 1; rotate 1
            copy
            editdel
            movesel -3 0
            paste
            selextend
          ]
        ]
      ]
    ]
  ] 0
]

.cubedeformer.ramp = [
  gotoface_fixpitch 0 [
    gotoedge 2 [
      editface 5 2
      gotoedge 3 [
        editface 5 2
        copy
        movesel 1 0
        paste
        gotoedge 0 [
          editface 5 2
          gotoedge 1 [
            editface 5 2
            gotoedge 2 [
              editface 8 2
              gotoedge 3 [
                editface 8 2
                copy
                movesel -2 0
                movesel 1 2
                paste
                gotoedge 2 [
                  editface -8 2
                  gotoedge 3 [
                    editface -8 2
                    copy
                    movesel 1 2
                    paste
                    editface 5 0
                    movesel -2 2
                    movesel 2 0
                    gotoface 2 [
                      reorient
                      rotate -1
                      movesel -1 0
                      rotate -1
                      movesel -1 0
                      editface 1 1
                      editface -1 1
                    ]
                  ]
                ]
              ]
            ]
          ]
        ]
      ]
    ]
  ] 0
]

.cubedeformer.cylinder_slow = [
  allfaces 1
  gotoface_fixpitch 0 [
    editfaceedge 0 1 2 [
      editfaceedge 0 3 2 [
        editfaceedge 3 0 2 [
          editfaceedge 3 2 2 [
            vcolor 1 0 0
            copy; movesel 1 1; paste; flip
            gotoface 0 [
              gotoselcenter 0 [
                vcolor 0 1 0
                copy; movesel -1 0; paste; flip
                gotoface 2 [
                  gotoselcenter 0 [
                    vcolor 0 0 1
                    copy; movesel -1 1; paste; flip
                    vcolor 1 0 1
                  ]
                ] 0
              ]
            ]
          ]
        ]
      ]
    ]
  ]
]

.cubedeformer.cylinder_fast = [
  gotoface 0 [
    gotoedge 1 [
      editface 2 2
      rotate -1
      editface 2 2
      rotate 1
      sleep 400 [
        gotoface 3 [
          gotoedge 0 [
            editface 2 2
            rotate 1
            editface 2 2
            rotate -1
            copy
            movesel 1 1
            paste
            flip
            selextend
            copy
            movesel -1 0
            paste
            rotate 1; rotate 1
          ]
        ]
      ]
    ]
  ] 0
]

.cubedeformer.smoothercylinder = [
  gotoface_fixpitch 0 [
    gotoedge 1 [
      gotoedge 1 [
        gridpower (+ $gridpower 1); reorient
        sleep 0 [
          editface 8 2
          rotate -1
          editface 8 2
          rotate -1
          editface 2 2
          rotate -1
          editface 2 2
          rotate -1
          movesel 1 1
          editface 2 2
          rotate -1
          editface 2 2
          rotate 1
          sleep 300 [
            gotoface 3 [
              selextend
              gotoedge 1 [
                editface 2 2
                rotate -1
                editface 2 2
                rotate 1
                movesel -1 0
                editface 2 2
                rotate -1
                editface 2 2
                rotate -1
                movesel 1 1
                selextend
                copy
                movesel 2 1
                paste
                flip
                selextend
                copy
                movesel -2 0
                movesel -1 2
                paste
                rotate 1; rotate 1
                selextend
                copy
                movesel -1 2
                paste
              ]
            ]
          ]
        ]
      ] 1
    ] 1
  ] 0
]

.cubedeformer.emerald = [
  allfaces 1
  gotoface_fixpitch 0 [
    gotoedge 0 [
      editface 4 2
      gotoedge 1 [
        editface 4 2
        gotoedge 3 [
          editface 4 2
          gotoface 1 [
            gotoedge 0 [
              editface 4 2
              gotoedge 1 [
                editface 4 2
                gotoedge 2 [
                  editface 4 2
                  gotoface 3 [
                    gotoedge 0 [
                      editface 8 2
                      gotoedge 2 [
                        editface 4 2
                        alpha; valpha 0.8 1; vscroll 0 999
                        flip; rotate 1; rotate 1; copy; vcolor 1 0 0
                        movesel -1 1; paste; flip; copy; vcolor 0 1 0
                        gotoselcenter 0
                        movesel 1 2; paste; rotate 1; rotate 1; copy; vcolor 0 0 1
                        movesel 1 1; paste; flip; vcolor 1 0 1
                      ]
                    ]
                  ] 300
                ]
              ]
            ]
          ] 300
        ]
      ]
    ]
  ] 300
]

.cubedeformer.pankubox = [
  allfaces 1
  gotoface_fixpitch 0 [
    vcolor 0 0 1
    editface 4 0
    gotoedge 0 [
      editface -4 2
      gotoedge 3 [
        editface 4 2
        rotate 1
        gotoface 3 [
          gotoedge 2 [
            allfaces 0
            vcolor 1 1 0
            editface 3 2
            flip
            vcolor 0 1 0
            flip
            gotoface 0 [
              rotate -1
              gotoface 3 [
                gotoedge 0 [
                  reorient
                  editface 4 2
                  gotoedge 2 [
                    editface 4 2
                    copy
                    movesel 1 1
                    paste
                    flip
                    selextend
                    copy
                    movesel -1 0
                    paste
                    gotoface 1 [
                      movesel 1 1
                      selextend
                      flip
                      movesel 1 0
                      selextend
                      copy
                      movesel 1 2
                      paste
                      rotate 1; rotate 1
                      copy
                      editdel
                      movesel -1 2
                      paste
                    ] 150
                  ]
                ]
              ] 300
            ] 500
          ]
        ] 400
      ]
    ]
  ] 300
]

.cubedeformer.halfclip = [
  gotoface_fixpitch 0 [
    gotoedge 1 [
      editface 7 2
      gotoedge 3 [
        editface 7 2
        gotoface 3 [
          gotoedge 0 [
            editface 8 2
            gotoedge 2 [
              editface 8 2
              gotoface 0 [
                gotoedge 1 [
                  editface 1 2
                  gotoedge 3 [
                    editface 1 2
                    rotate -1
                    allfaces 1
                    alpha
                    valpha 0.001
                    sleep 0 [
                      movesel -1 0
                      editface 1 1; editface -1 1; vcolor 0.2 0.2 0.2; editface 7 0
                      allfaces 0
                      vcolor 1 0 0; vscroll 0 999; flip
                      vcolor 0 1 0; vscroll 999
                    ]
                  ]
                ]
              ]
            ]
          ]
        ] 400
      ]
    ]
  ] 400
]
